
// -------------------- plug-ins -------------------- //

plugins {
	id 'com.softicar.gradle.java.library' version '4.0.3'
	id 'com.softicar.gradle.code.validation' version '4.0.3' apply false
	id 'com.softicar.gradle.dependency.validation' version '4.0.3' apply false
	id 'com.softicar.gradle.selenium.grid' version '4.0.3'
	id 'com.softicar.gradle.test.logger' version '4.0.3' apply false
}

// -------------------- dependencies -------------------- //

// The identifiers below correspond to artifact names in camelCase.
project.ext {
	versions = [
		asm: "9.0",
		byteBuddy: "1.10.18",
		classgraph: "4.8.90",
		commonsCodec: "1.15",
		commonsFileupload: "1.4",
		commonsLang3: "3.11",
		commonsNet: "3.7.2",
		commonsText: "1.9",
		gson: "2.8.7",
		h2: "1.4.199",
		hikariCp: "3.4.5",
		imgscalr: "4.2", // no longer maintained - this version dates back to 2012
		javaxServletApi: "4.0.1",
		jcifs: "1.3.17",
		jetty: "9.4.34.v20201102",
		jsch: "0.1.55",
		junit: "4.13.1",
		mockito: "3.6.0",
		pdfbox: "2.0.24",
		poi: "4.1.2",
		selenium: "3.141.59",
		sunMail: "1.6.5",
		tesseractPlatform: "4.1.1-1.5.4",
		zxing: "3.4.1",
	]
}

if(project.hasProperty('dependencyProxy')) {
	println "Using dependency proxy: $dependencyProxy"
	subprojects {
		repositories {
			maven {
				allowInsecureProtocol true
				url project['dependencyProxy']
			}
		}
	}
} else {
	subprojects {
		repositories {
			mavenCentral()
		}
	}
}

subprojects {
	configurations.all {
		resolutionStrategy {
			failOnVersionConflict()
			force "commons-codec:commons-codec:$versions.commonsCodec"
			force "javax.servlet:javax.servlet-api:$versions.javaxServletApi"
			force "net.bytebuddy:byte-buddy:$versions.byteBuddy"
		}
	}

	task allDependencies(type: DependencyReportTask) {
		// nothing
	}

	apply plugin: 'com.softicar.gradle.dependency.validation'
	check.dependsOn softicarDependencyValidation
}

// -------------------- compile -------------------- //

subprojects {
	apply plugin: 'com.softicar.gradle.java.library' 

	sourceCompatibility = "15"
	targetCompatibility = "15"

	compileJava {
		options.encoding = "UTF-8"
	}

	compileTestJava {
		options.encoding = "UTF-8"
	}

	processResources {
		from('src/main/java') {
			include '**/*.sqml'
		}
	}
}

// -------------------- test -------------------- //

subprojects {
	apply plugin: 'com.softicar.gradle.test.logger'

	test {
		maxHeapSize = "2G"
		systemProperty 'java.awt.headless', 'true'
	}

	dependencies {
		testImplementation "junit:junit:$versions.junit"
		testImplementation "org.mockito:mockito-core:$versions.mockito"
	}
}

// -------------------- validation -------------------- //

subprojects {
	apply plugin: 'com.softicar.gradle.code.validation'

	softicarCodeValidationSettings {
		validationEntryPointClass = "com.softicar.platform.common.core.java.code.validation.JavaCodeValidationEntryPoint"
		arguments = [ "--mandatoryTranslations", "de" ]
	}

	check.dependsOn softicarCodeValidation
}

// -------------------- publishing -------------------- //

// TODO Replace "ossrh" with "publish" or "publish_remote"
def OSSRH_TARGET = findProperty('ossrhTarget')
def OSSRH_URL = findProperty('ossrhUrl')
def OSSRH_USERNAME = findProperty('ossrhUsername')
def OSSRH_PASSWORD = findProperty('ossrhPassword')
def SIGNING_KEY_ID = findProperty('signing.keyId')
def SIGNING_PASSWORD = findProperty('signing.password')
def SIGNING_SECRET_KEY_RING_FILE = findProperty('signing.secretKeyRingFile')

subprojects {
	apply plugin: 'maven-publish'
	apply plugin: 'signing'

	javadoc {
		options.encoding = 'UTF-8'
	}
	java {
		withJavadocJar()
		withSourcesJar()
	}
	publishing {
		repositories {
			if(OSSRH_TARGET == 'remote') {
				maven {
					if(OSSRH_URL != null) {
						url = OSSRH_URL
						if(OSSRH_USERNAME != null) {
							credentials(PasswordCredentials) {
								username OSSRH_USERNAME
								password OSSRH_PASSWORD
							}
						}
					}
				}
			} else if(OSSRH_TARGET == 'local') {
				maven {
					url = System.properties['user.home'] + '/.softicar/maven-local'
				}
			}
		}
		publications {
			mavenJava(MavenPublication) {
				from components.java
				pom {
					name = 'SoftiCAR Platform'
					description = 'The SoftiCAR Platform is a lightweight, Java-based library to create interactive business web applications.'
					url = 'https://github.com/softicar/platform'
					developers {
						developer {
							name = 'SoftiCAR'
							email = 'opensource@softicar.com'
							organization = 'SoftiCAR'
							organizationUrl = 'https://github.com/softicar'
						}
					}
					licenses {
						license {
							name = 'MIT License'
							url = 'https://github.com/softicar/platform/blob/main/LICENSE'
						}
					}
					scm {
						connection = 'scm:git:git://github.com/softicar/platform.git'
						developerConnection = 'scm:git:ssh://github.com:softicar/platform.git'
						url = 'https://github.com/softicar/platform/tree/main'
					}
				}
			}
		}
	}

	signing {
		if(OSSRH_TARGET == 'remote') {
			sign publishing.publications.mavenJava
		}
	}

	publish.doFirst {
		if(OSSRH_TARGET != 'remote' && OSSRH_TARGET != 'local') {
			throw new GradleException("Please specify the publication target with '-PossrhTarget=[remote|local]'.")
		}
		if(version == "unspecified") {
			throw new GradleException("Please specify the version to publish with '-Pversion=X.Y.Z'.")
		}
	}

	// This task is created dynamically, upon a valid 'signing' definition.
	if(tasks.findByName('signMavenJavaPublication')) {
		signMavenJavaPublication.doFirst {
			if(OSSRH_TARGET == 'remote') {
				if(SIGNING_KEY_ID == null) {
					throw new GradleException("Please specify the PGP signing key ID with 'signing.keyId' in '~/.gradle/gradle.properties'.")
				}
				// TODO Fix inconsistent naming:
				// (1) In a PGP context, the term of "passphrase" is preferred over "password"
				// (2) Should be named "signing KEY passphrase", instead of "signing passphrase", because the passphrase strongly relates to the key.
				if(SIGNING_PASSWORD == null) {
					throw new GradleException("Please specify the PGP signing key passphrase with 'signing.password' in '~/.gradle/gradle.properties'.")
				}
				if(SIGNING_SECRET_KEY_RING_FILE == null) {
					throw new GradleException("Please specify the PGP secret key ring file with 'signing.secretKeyRingFile' in '~/.gradle/gradle.properties'.")
				}
			}
		}
	}

	// This task is created dynamically, upon a valid 'publishing' definition.
	if(tasks.findByName('publishMavenJavaPublicationToMavenRepository')) {
		publishMavenJavaPublicationToMavenRepository.doFirst {
			if(OSSRH_TARGET != 'remote' && OSSRH_TARGET != 'local') {
				throw new GradleException("Please specify the publication target with '-PossrhTarget=[remote|local]'.")
			}
			if(version == "unspecified") {
				throw new GradleException("Please specify the version to publish with '-Pversion=X.Y.Z'.")
			}
			if(OSSRH_TARGET == 'remote') {
				if(OSSRH_URL == null) {
					throw new GradleException("Please specify the remote publication URL with 'ossrhUrl' in '~/.gradle/gradle.properties'.")
				}
				if(OSSRH_USERNAME == null) {
					throw new GradleException("Please specify the remote publication username with 'ossrhUsername' in '~/.gradle/gradle.properties'.")
				}
				if(OSSRH_PASSWORD == null) {
					throw new GradleException("Please specify the remote publication password with 'ossrhPassword' in '~/.gradle/gradle.properties'.")
				}
			}
		}
	}
}

// -------------------- Eclipse access rules -------------------- //

import org.gradle.plugins.ide.eclipse.model.AccessRule

String JRE_CONTAINER = 'org.eclipse.jdt.launching.JRE_CONTAINER'
String BUILDSHIP_CONTAINER_PATH = 'org.eclipse.buildship.core.gradleclasspathcontainer'

subprojects {
	apply plugin: 'eclipse'

	eclipse.classpath {
		containers BUILDSHIP_CONTAINER_PATH
		file {
			whenMerged {
				def jre = entries.find { it.path.startsWith(JRE_CONTAINER) }
				jre.accessRules.add(new AccessRule('1', 'com/sun/**'))
				jre.accessRules.add(new AccessRule('1', 'java/awt/List'))
				jre.accessRules.add(new AccessRule('1', 'jdk/**'))
				jre.accessRules.add(new AccessRule('1', 'sun/**'))
			
				def buildship = entries.find { it.path.equals(BUILDSHIP_CONTAINER_PATH) }
				buildship.accessRules.add(new AccessRule('1', '**/Collection'))
				buildship.accessRules.add(new AccessRule('1', '**/Function'))
				buildship.accessRules.add(new AccessRule('1', '**/Predicate'))
				buildship.accessRules.add(new AccessRule('1', '**/Supplier'))
				buildship.accessRules.add(new AccessRule('1', '**/Objects'))
				buildship.accessRules.add(new AccessRule('1', '**/Optional'))
				buildship.accessRules.add(new AccessRule('1', 'junit/**'))
				buildship.accessRules.add(new AccessRule('1', 'org/apache/commons/logging/**'))
				buildship.accessRules.add(new AccessRule('1', 'org/apache/poi/ss/formula/functions/T'))
				buildship.accessRules.add(new AccessRule('1', 'org/apache/xmlbeans/impl/**'))
			}
		}
	}
}
