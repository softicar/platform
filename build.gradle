// -------------------- plug-ins -------------------- //

plugins {
	id 'com.softicar.gradle.java.library' version '4.0.5'
	id 'com.softicar.gradle.code.validation' version '4.0.5' apply false
	id 'com.softicar.gradle.dependency.validation' version '4.0.5' apply false
	id 'com.softicar.gradle.selenium.grid' version '4.0.5'
	id 'com.softicar.gradle.test.logger' version '4.0.5' apply false
	id "com.github.ben-manes.versions" version '0.52.0'
}

// -------------------- dependencies -------------------- //

// The identifiers below correspond to artifact names in camelCase.
project.ext {
	versions = [
		angusMail: "2.0.3",
		asm: "9.8",
		bcprov: "1.81",
		byteBuddy: "1.17.5",
		classgraph: "4.8.179",
		commonsCodec: "1.18.0",
		commonsCollections4: "4.5.0",
		commonsCompress: "1.27.1",
		commonsIo: "2.19.0",
		commonsLang3: "3.17.0",
		commonsNet: "3.11.1",
		commonsText: "1.13.1",
		errorProneAnnotations: "2.38.0",
		flyingSaucerPdf: "9.12.0",
		gson: "2.13.1",
		guava: "33.4.8-jre",
		h2: "2.3.232",
		hikariCp: "6.3.0",
		imageioJpeg: "3.12.0",
		// optional dependency of pdfbox; enables rendering of jpeg2000-encoded embedded images
		jaiImageioJpeg2000: "1.4.0",
		jakartaActivation: "2.1.3",
		jakartaMail: "2.1.3",
		jakartaServletApi: "6.0.0",
		jcifsNg: "2.1.10",
		jettyServlet: "12.0.22",
		jettyServer: "12.0.22",
		jsch: "2.27.2",
		jsonPath: "2.9.0",
		jsonSmart: "2.5.2",
		jsoup: "1.20.1",
		junit: "4.13.2",
		log4j: "2.24.3",
		mariadbJavaClient: "2.7.12",
		mockito: "5.18.0",
		netty: "4.2.2.Final",
		pdfbox: "3.0.5",
		poi: "5.4.1",
		selenium: "4.33.0",
		simpleJavaMail: "8.12.6",
		slf4j: "2.0.17",
		tesseract: "5.5.0-1.5.11",
		tinymce: "6.8.3",
	]
}

if(project.hasProperty('dependencyProxy')) {
	println "Using dependency proxy: $dependencyProxy"
	subprojects {
		repositories {
			maven {
				setAllowInsecureProtocol(true)
				setUrl(project['dependencyProxy'])
			}
			mavenCentral()
			maven { setUrl("https://repository.jboss.org/maven2/")  }
			gradlePluginPortal()
		}
	}
} else {
	subprojects {
		repositories {
			mavenCentral()
			maven { setUrl("https://repository.jboss.org/maven2/")  }
			gradlePluginPortal()
		}
	}
}

subprojects {
	configurations.all {
		resolutionStrategy {
			failOnVersionConflict()
			// versions below 30.0 have known vulnerabilities
			force "com.google.guava:guava:$versions.guava"
			force "com.sun.activation:jakarta.activation:$versions.jakartaActivation"
			force "commons-codec:commons-codec:$versions.commonsCodec"
			force "jakarta.servlet:jakarta.servlet-api:$versions.jakartaServletApi"
			force "net.bytebuddy:byte-buddy:$versions.byteBuddy"
			force "net.bytebuddy:byte-buddy-agent:$versions.byteBuddy"
			// versions below 1.21 have known vulnerabilities
			force "org.apache.commons:commons-compress:$versions.commonsCompress"
			force "org.apache.logging.log4j:log4j-api:$versions.log4j"
			force "org.bouncycastle:bcprov-jdk18on:$versions.bcprov"
			force "org.slf4j:slf4j-api:$versions.slf4j"

			force "net.minidev:json-smart:$versions.jsonSmart"
			force "com.google.errorprone:error_prone_annotations:$versions.errorProneAnnotations"
			force "org.apache.commons:commons-lang3:$versions.commonsLang3"
			force "org.apache.poi:poi:$versions.poi"
			force "org.apache.poi:poi-ooxml:$versions.poi"
			force "org.apache.poi:poi-scratchpad:$versions.poi"
			force "org.ow2.asm:asm:$versions.asm"
			force "commons-io:commons-io:$versions.commonsIo"
			force "junit:junit:$versions.junit"
			force "com.github.jai-imageio:jai-imageio-jpeg2000:$versions.jaiImageioJpeg2000"
			force "com.twelvemonkeys.imageio:imageio-jpeg:$versions.imageioJpeg"
			force "com.github.mwiede:jsch:$versions.jsch"

			force "com.google.code.gson:gson:$versions.gson"
			force "com.h2database:h2:$versions.h2"
			force "com.jayway.jsonpath:json-path:$versions.jsonPath"
			force "com.zaxxer:HikariCP:$versions.hikariCp"
			force "commons-net:commons-net:$versions.commonsNet"
			force "eu.agno3.jcifs:jcifs-ng:$versions.jcifsNg"
			force "io.github.classgraph:classgraph:$versions.classgraph"
			force "jakarta.mail:jakarta.mail-api:$versions.jakartaMail"
			force "org.apache.commons:commons-collections4:$versions.commonsCollections4"
			force "org.apache.commons:commons-text:$versions.commonsText"
			force "org.apache.logging.log4j:log4j-core:$versions.log4j"
			force "org.apache.pdfbox:pdfbox:$versions.pdfbox"
			force "org.bytedeco:tesseract-platform:$versions.tesseract"
			force "org.eclipse.angus:angus-mail:$versions.angusMail"
			force "org.eclipse.jetty.ee10:jetty-ee10-servlet:$versions.jettyServlet"
			force "org.jsoup:jsoup:$versions.jsoup"
			force "org.mariadb.jdbc:mariadb-java-client:$versions.mariadbJavaClient"
			force "org.mockito:mockito-core:$versions.mockito"
			force "org.seleniumhq.selenium:selenium-chrome-driver:$versions.selenium"
			force "org.seleniumhq.selenium:selenium-support:$versions.selenium"
			force "org.simplejavamail:outlook-module:$versions.simpleJavaMail"
			force "org.simplejavamail:simple-java-mail:$versions.simpleJavaMail"
			force "org.slf4j:slf4j-simple:$versions.slf4j"
			force "org.webjars.bower:tinymce:$versions.tinymce"
			force "org.xhtmlrenderer:flying-saucer-pdf:$versions.flyingSaucerPdf"

			eachDependency { details ->
				// force version for most 'io.netty' dependencies
				if (details.requested.group == 'io.netty') {
					if(details.requested.name != 'netty-tcnative-classes') {
						details.useVersion "$versions.netty"
					}
				}
			}
		}
	}

	task allDependencies(type: DependencyReportTask) {
		// nothing
	}

	apply plugin: 'com.softicar.gradle.dependency.validation'
	check.dependsOn softicarDependencyValidation

}

// -------------------- compile -------------------- //

//apply plugin: 'com.github.ben-manes.versions'

java {
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

subprojects {
	apply plugin: 'com.softicar.gradle.java.library'

	java {
		sourceCompatibility = JavaVersion.VERSION_21
		targetCompatibility = JavaVersion.VERSION_21
	}

	compileJava {
		sourceCompatibility = JavaVersion.VERSION_21
		targetCompatibility = JavaVersion.VERSION_21
		options.encoding = "UTF-8"
	}

	compileTestJava {
		sourceCompatibility = JavaVersion.VERSION_21
		targetCompatibility = JavaVersion.VERSION_21
		options.encoding = "UTF-8"
	}

	processResources {
		from('src/main/java') {
			include '**/*.sqml'
		}
	}
}

// -------------------- test -------------------- //

subprojects {
	apply plugin: 'com.softicar.gradle.test.logger'

	test {
		maxHeapSize = "2G"
		systemProperty 'java.awt.headless', 'true'
	}

	dependencies {
		testImplementation "junit:junit:$versions.junit"
		testImplementation "org.mockito:mockito-core:$versions.mockito"
	}
}

// -------------------- publishing -------------------- //

def PUBLISH_TARGET = findProperty('publish.target')
def PUBLISH_URL = findProperty('publish.url')
def PUBLISH_USERNAME = findProperty('publish.username')
def PUBLISH_PASSWORD = findProperty('publish.password')
def SIGNING_KEY_ID = findProperty('signing.keyId')
def SIGNING_PASSWORD = findProperty('signing.password')
def SIGNING_SECRET_KEY_RING_FILE = findProperty('signing.secretKeyRingFile')

subprojects {
	apply plugin: 'maven-publish'
	apply plugin: 'signing'

	javadoc {
		options.encoding = 'UTF-8'
	}
	java {
		withJavadocJar()
		withSourcesJar()
	}
	publishing {
		repositories {
			if(PUBLISH_TARGET == 'remote') {
				maven {
					if(PUBLISH_URL != null) {
						url = PUBLISH_URL
						if(PUBLISH_USERNAME != null) {
							credentials(PasswordCredentials) {
								username PUBLISH_USERNAME
								password PUBLISH_PASSWORD
							}
						}
					}
				}
			} else if(PUBLISH_TARGET == 'local') {
				maven {
					url = System.properties['user.home'] + '/.softicar/maven-local'
				}
			}
		}
		publications {
			mavenJava(MavenPublication) {
				from components.java
				pom {
					name = 'SoftiCAR Platform'
					description = 'The SoftiCAR Platform is a lightweight, Java-based library to create interactive business web applications.'
					url = 'https://github.com/softicar/platform'
					developers {
						developer {
							name = 'SoftiCAR'
							email = 'opensource@softicar.com'
							organization = 'SoftiCAR'
							organizationUrl = 'https://github.com/softicar'
						}
					}
					licenses {
						license {
							name = 'MIT License'
							url = 'https://github.com/softicar/platform/blob/main/LICENSE'
						}
					}
					scm {
						connection = 'scm:git:git://github.com/softicar/platform.git'
						developerConnection = 'scm:git:ssh://github.com:softicar/platform.git'
						url = 'https://github.com/softicar/platform/tree/main'
					}
				}
			}
		}
	}

	signing {
		if(PUBLISH_TARGET == 'remote') {
			sign publishing.publications.mavenJava
		}
	}

	publish.doFirst {
		if(PUBLISH_TARGET != 'remote' && PUBLISH_TARGET != 'local') {
			throw new GradleException("Please specify the publication target with '-Ppublish.target=[remote|local]'.")
		}
		if(version == "unspecified") {
			throw new GradleException("Please specify the version to publish with '-Pversion=X.Y.Z'.")
		}
	}

	// This task is created dynamically, upon a valid 'signing' definition.
	if(tasks.findByName('signMavenJavaPublication')) {
		signMavenJavaPublication.doFirst {
			if(PUBLISH_TARGET == 'remote') {
				if(SIGNING_KEY_ID == null) {
					throw new GradleException("Please specify the PGP signing key ID with 'signing.keyId' in '~/.gradle/gradle.properties'.")
				}
				if(SIGNING_PASSWORD == null) {
					throw new GradleException("Please specify the PGP secret key password with 'signing.password' in '~/.gradle/gradle.properties'.")
				}
				if(SIGNING_SECRET_KEY_RING_FILE == null) {
					throw new GradleException("Please specify the PGP secret key ring file with 'signing.secretKeyRingFile' in '~/.gradle/gradle.properties'.")
				}
			}
		}
	}

	// This task is created dynamically, upon a valid 'publishing' definition.
	if(tasks.findByName('publishMavenJavaPublicationToMavenRepository')) {
		publishMavenJavaPublicationToMavenRepository.doFirst {
			if(PUBLISH_TARGET != 'remote' && PUBLISH_TARGET != 'local') {
				throw new GradleException("Please specify the publication target with '-Ppublish.target=[remote|local]'.")
			}
			if(version == "unspecified") {
				throw new GradleException("Please specify the version to publish with '-Pversion=X.Y.Z'.")
			}
			if(PUBLISH_TARGET == 'remote') {
				if(PUBLISH_URL == null) {
					throw new GradleException("Please specify the remote publication URL with 'publish.url' in '~/.gradle/gradle.properties'.")
				}
				if(PUBLISH_USERNAME == null) {
					throw new GradleException("Please specify the remote publication username with 'publish.username' in '~/.gradle/gradle.properties'.")
				}
				if(PUBLISH_PASSWORD == null) {
					throw new GradleException("Please specify the remote publication password with 'publish.password' in '~/.gradle/gradle.properties'.")
				}
			}
		}
	}
}

// -------------------- Eclipse access rules -------------------- //

import org.gradle.plugins.ide.eclipse.model.AccessRule

String JRE_CONTAINER = 'org.eclipse.jdt.launching.JRE_CONTAINER'
String BUILDSHIP_CONTAINER_PATH = 'org.eclipse.buildship.core.gradleclasspathcontainer'

subprojects {
	apply plugin: 'eclipse'

	eclipse.classpath {
		containers BUILDSHIP_CONTAINER_PATH
		file {
			whenMerged {
				def jre = entries.find { it.path.startsWith(JRE_CONTAINER) }
				jre.accessRules.add(new AccessRule('1', 'java/applet/**'))
				jre.accessRules.add(new AccessRule('1', 'java/awt/List'))
				jre.accessRules.add(new AccessRule('1', 'javax/swing/**'))
				jre.accessRules.add(new AccessRule('1', 'javax/sql/rowset/Predicate'))
				jre.accessRules.add(new AccessRule('1', 'jdk/**'))
				jre.accessRules.add(new AccessRule('1', 'org/graalvm/**'))
				jre.accessRules.add(new AccessRule('1', 'sun/**'))
			
				def buildship = entries.find { it.path.equals(BUILDSHIP_CONTAINER_PATH) }
				buildship.accessRules.add(new AccessRule('1', '**/BiConsumer'))
				buildship.accessRules.add(new AccessRule('1', '**/Collection'))
				buildship.accessRules.add(new AccessRule('1', '**/Consumer'))
				buildship.accessRules.add(new AccessRule('1', '**/Function'))
				buildship.accessRules.add(new AccessRule('1', '**/Iterable'))
				buildship.accessRules.add(new AccessRule('1', '**/Iterator'))
				buildship.accessRules.add(new AccessRule('1', '**/List'))
				buildship.accessRules.add(new AccessRule('1', '**/Map'))
				buildship.accessRules.add(new AccessRule('1', '**/Objects'))
				buildship.accessRules.add(new AccessRule('1', '**/Optional'))
				buildship.accessRules.add(new AccessRule('1', '**/Predicate'))
				buildship.accessRules.add(new AccessRule('1', '**/Set'))
				buildship.accessRules.add(new AccessRule('1', '**/Supplier'))
				buildship.accessRules.add(new AccessRule('1', 'com/google/**/Charsets'))
				buildship.accessRules.add(new AccessRule('1', 'junit/**'))
				buildship.accessRules.add(new AccessRule('1', 'org/**/Log'))
				buildship.accessRules.add(new AccessRule('1', 'org/**/Pair'))
				buildship.accessRules.add(new AccessRule('1', 'org/apache/**/Charsets'))
				buildship.accessRules.add(new AccessRule('1', 'org/apache/commons/logging/**'))
				buildship.accessRules.add(new AccessRule('1', 'org/apache/poi/ss/formula/functions/T'))
				buildship.accessRules.add(new AccessRule('1', 'org/apache/xmlbeans/impl/**'))
			}
		}
	}
}
